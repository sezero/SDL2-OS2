# sdl2_rtf cmake project-config input for ./configure scripts

include(FeatureSummary)
set_package_properties(SDL2_rtf PROPERTIES
    URL "https://www.libsdl.org/projects/SDL_rtf/"
    DESCRIPTION "Support for Rich Text Format (.rtf) files with Simple Directmedia Layer"
)

set(SDL2_rtf_FOUND TRUE)

get_filename_component(CMAKE_CURRENT_LIST_DIR ${CMAKE_CURRENT_LIST_DIR} REALPATH)
get_filename_component(prefix "${CMAKE_CURRENT_LIST_DIR}@cmake_prefix_relpath@" ABSOLUTE)
set(exec_prefix "@exec_prefix@")
set(bindir "@bindir@")
set(includedir "@includedir@")
set(libdir "@libdir@")
set(_sdl2rtf_extra_static_libraries "@RTF_LIBS@ @PC_LIBS@")
string(STRIP "${_sdl2rtf_extra_static_libraries}" _sdl2rtf_extra_static_libraries)

set(_sdl2rtf_bindir   "${bindir}")
set(_sdl2rtf_libdir   "${libdir}")
set(_sdl2rtf_incdir   "${includedir}/SDL2")

# Convert _sdl2rtf_extra_static_libraries to list and keep only libraries
string(REGEX MATCHALL "(-[lm]([-a-zA-Z0-9._]+))|(-Wl,[^ ]*framework[^ ]*)" _sdl2rtf_extra_static_libraries "${_sdl2rtf_extra_static_libraries}")
string(REGEX REPLACE "^-l" "" _sdl2rtf_extra_static_libraries "${_sdl2rtf_extra_static_libraries}")
string(REGEX REPLACE ";-l" ";" _sdl2rtf_extra_static_libraries "${_sdl2rtf_extra_static_libraries}")

unset(prefix)
unset(exec_prefix)
unset(bindir)
unset(includedir)
unset(libdir)

include(CMakeFindDependencyMacro)

if(NOT TARGET SDL2_rtf::SDL2_rtf)
    if(WIN32)
        set(_sdl2rtf_dll "${_sdl2rtf_bindir}/SDL2_rtf.dll")
        set(_sdl2rtf_imp "${_sdl2rtf_libdir}/${CMAKE_STATIC_LIBRARY_PREFIX}SDL2_rtf.dll${CMAKE_STATIC_LIBRARY_SUFFIX}")
        if(EXISTS "${_sdl2rtf_dll}" AND EXISTS "${_sdl2rtf_imp}")
            add_library(SDL2_rtf::SDL2_rtf SHARED IMPORTED)
            set_target_properties(SDL2_rtf::SDL2_rtf
                PROPERTIES
                    IMPORTED_LOCATION "${_sdl2rtf_dll}"
                    IMPORTED_IMPLIB "${_sdl2rtf_imp}"
            )
        endif()
        unset(_sdl2rtf_dll)
        unset(_sdl2rtf_imp)
    else()
        set(_sdl2rtf_shl "${_sdl2rtf_libdir}/${CMAKE_SHARED_LIBRARY_PREFIX}SDL2_rtf${CMAKE_SHARED_LIBRARY_SUFFIX}")
        if(EXISTS "${_sdl2rtf_shl}")
            add_library(SDL2_rtf::SDL2_rtf SHARED IMPORTED)
            set_target_properties(SDL2_rtf::SDL2_rtf
                PROPERTIES
                    IMPORTED_LOCATION "${_sdl2rtf_shl}"
            )
        endif()
    endif()
    if(TARGET SDL2_rtf::SDL2_rtf)
        set_target_properties(SDL2_rtf::SDL2_rtf
            PROPERTIES
                INTERFACE_INCLUDE_DIRECTORIES "${_sdl2rtf_incdir}"
                COMPATIBLE_INTERFACE_BOOL "SDL2_SHARED"
                INTERFACE_SDL2_SHARED "ON"
        )
    endif()
endif()

if(NOT TARGET SDL2_rtf::SDL2_rtf-static)
    set(_sdl2rtf_stl "${_sdl2rtf_libdir}/${CMAKE_STATIC_LIBRARY_PREFIX}SDL2_rtf${CMAKE_STATIC_LIBRARY_SUFFIX}")
    if(EXISTS "${_sdl2rtf_stl}")
        add_library(SDL2_rtf::SDL2_rtf-static STATIC IMPORTED)
        set_target_properties(SDL2_rtf::SDL2_rtf-static
            PROPERTIES
                INTERFACE_INCLUDE_DIRECTORIES "${_sdl2rtf_incdir}"
                IMPORTED_LOCATION "${_sdl2rtf_stl}"
                INTERFACE_LINK_LIBRARIES "${_sdl2rtf_extra_static_libraries}"
        )
    endif()
    unset(_sdl2rtf_stl)
endif()

unset(_sdl2rtf_extra_static_libraries)
unset(_sdl2rtf_bindir)
unset(_sdl2rtf_libdir)
unset(_sdl2rtf_incdir)
